import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id "com.exactpro.th2.gradle.component" version "0.0.7"
    id 'application'
}

ext {
    dockerImageVersion = release_version
    cradleVersion      = '5.3.0-dev'
}

group 'com.exactpro.th2'
version release_version

kotlin {
    jvmToolchain(11)
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    implementation 'org.slf4j:slf4j-api'

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation group: 'net.jpountz.lz4', name: 'lz4', version: '1.3.0'

    implementation 'org.apache.commons:commons-lang3'

    implementation('com.exactpro.th2:common:5.11.0-dev') {
        exclude group: 'com.exactpro.th2', module: 'cradle-core'
        exclude group: 'com.exactpro.th2', module: 'cradle-cassandra'
    }
    implementation 'com.exactpro.th2:common-utils:2.2.3-dev'

    implementation "com.exactpro.th2:cradle-core:${cradleVersion}"

    implementation "com.exactpro.th2:cradle-cassandra:${cradleVersion}"

    implementation 'com.exactpro.th2:grpc-data-provider:0.2.0-dev'

    implementation 'io.github.microutils:kotlin-logging:3.0.5'

    implementation 'io.prometheus:simpleclient'

    implementation(platform('io.ktor:ktor-bom:2.3.3'))
    implementation 'io.ktor:ktor-server-netty'
    implementation 'io.ktor:ktor-server'

    implementation 'org.ehcache:ehcache:3.8.1'
    implementation('org.glassfish.jaxb:jaxb-runtime:2.3.9') {
        because("'2.3.9' version has 'EDL 1.0' license instead of 'CDDL GPL 1.1' in the '2.3.1'")
    }

    testImplementation('io.mockk:mockk:1.12.4') { // The last version bases on kotlin 1.6.0
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect' // because we need kotlin reflect with different version
    }
    testImplementation('org.jetbrains.kotlin:kotlin-reflect') {
        because('mockk needs it')
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.2"

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs.add('-opt-in=kotlin.RequiresOptIn')
    }
}

application {
    mainClassName = "com.exactpro.th2.rptdataprovider.MainKt"
}


test {
// FIXME: the tests were temporary disabled since they're not compatible with new api

//    useJUnitPlatform()
}

dependencyCheck {
    suppressionFile='supressions.xml'
}