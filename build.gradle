import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id "com.exactpro.th2.gradle.component" version "0.3.10"
    id 'application'
}

ext {
    dockerImageVersion = release_version
    cradleVersion      = '5.7.0-dev'
}

group 'com.exactpro.th2'
version release_version

th2JavaRelease {
    targetJavaVersion.set(JavaVersion.VERSION_11)
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    implementation 'org.slf4j:slf4j-api'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.apache.commons:commons-lang3'

    implementation('com.exactpro.th2:common:5.17.0-dev') {
        exclude group: 'com.exactpro.th2', module: 'cradle-core'
        exclude group: 'com.exactpro.th2', module: 'cradle-cassandra'
    }

    implementation 'com.exactpro.th2:common-utils:2.4.0-dev'
    implementation "com.exactpro.th2:cradle-core:${cradleVersion}"
    implementation "com.exactpro.th2:cradle-cassandra:${cradleVersion}"
    implementation 'com.exactpro.th2:grpc-data-provider:0.2.0-dev'

    implementation 'io.github.oshai:kotlin-logging:7.0.13'
    implementation 'io.prometheus:simpleclient'
    implementation(platform('io.ktor:ktor-bom:3.3.3'))
    implementation 'io.ktor:ktor-server-netty'
    implementation 'io.ktor:ktor-server'

    implementation('org.ehcache:ehcache:3.11.1') {
        exclude group: 'org.glassfish.jaxb', module: 'jaxb-runtime'
    }

    testImplementation('io.mockk:mockk:1.14.6') { // The last version bases on kotlin 1.6.0
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect' // because we need kotlin reflect with different version
    }

    testImplementation('org.jetbrains.kotlin:kotlin-reflect') {
        because('mockk needs it')
    }

    testImplementation(platform("org.junit:junit-bom:5.14.1"))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs.add('-opt-in=kotlin.RequiresOptIn')
    }
}

application.mainClass = "com.exactpro.th2.rptdataprovider.MainKt"
test.useJUnitPlatform()
dependencyCheck.suppressionFile = 'suppressions.xml'