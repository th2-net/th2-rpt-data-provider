/*******************************************************************************
 * Copyright 2009-2020 Exactpro (Exactpro Systems Limited)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
    id "org.owasp.dependencycheck" version "8.1.2"

}

ext {
    dockerImageVersion = release_version
    cradleVersion       = '5.0.2-dev-version-5-5401060951-c4bcb64-SNAPSHOT'
}

group 'com.exactpro.th2'
version release_version

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
}


dependencies {
    api platform('com.exactpro.th2:bom:4.2.0')

    implementation 'org.slf4j:slf4j-api'

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation group: 'net.jpountz.lz4', name: 'lz4', version: '1.3.0'

    implementation 'org.apache.commons:commons-lang3'

    implementation('com.exactpro.th2:common:5.2.0') {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
        exclude group: 'com.exactpro.th2', module: 'cradle-core'
        exclude group: 'com.exactpro.th2', module: 'cradle-cassandra'
    }

    implementation("com.exactpro.th2:cradle-core:${cradleVersion}") {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    implementation("com.exactpro.th2:cradle-cassandra:${cradleVersion}") {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'com.fasterxml.jackson.core'
    }

    implementation('org.apache.logging.log4j:log4j-core') {
        because('need to have log4j core inside the classpath')
    }
    implementation('org.apache.logging.log4j:log4j-slf4j2-impl') {
        because('need loging impl')
    }

    implementation 'com.exactpro.th2:grpc-data-provider:0.1.8'

    implementation 'io.github.microutils:kotlin-logging:2.1.23'

    implementation 'io.prometheus:simpleclient'

    implementation 'io.ktor:ktor-server-netty:1.6.8'
    implementation 'org.ehcache:ehcache:3.8.1'

    testImplementation('io.mockk:mockk:1.9.3') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect' // because we need kotlin reflect with different version
    }
    testImplementation('org.jetbrains.kotlin:kotlin-reflect') {
        because('mockk needs it')
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.1"

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

test {
// FIXME: the tests were temporary disabled since they're not compatible with new api

//    useJUnitPlatform()
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}


compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

application {
    mainClassName = "com.exactpro.th2.rptdataprovider.MainKt"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    suppressionFile='supressions.xml'
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}
